buildscript {
    ext.kotlin_version = '1.4.32'
    ext.testContainers_version = '1.15.2'
    ext.wiremock_version = '2.27.2'
    ext.klogging_version = '2.0.6'
    ext.libVersion = "3.0.0"
    ext.libGroup = 'io.github.adven27'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.14.2"
    }
}

group = libGroup
version = libVersion

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        jcenter {
            content {
                // Only download the 'kotlinx-html-jvm' module from JCenter, but nothing else.
                // detekt needs 'kotlinx-html-jvm' for the HTML report.
                includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
            }
        }
    }

    group = libGroup
    version = libVersion

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'utf-8'
    compileTestJava.options.encoding = 'utf-8'

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    apply from: "$rootDir/gradle/linters.gradle"
}

task publishMaven {
    dependsOn ":env-core:publish"
    dependsOn ":env-container:publish"
    dependsOn ":env-db-db2:publish"
    dependsOn ":env-db-mysql:publish"
    dependsOn ":env-db-postgresql:publish"
    dependsOn ":env-db-oracle:publish"
    dependsOn ":env-grpc-mock:publish"
    dependsOn ":env-mq-kafka:publish"
    dependsOn ":env-mq-kafka-embedded:publish"
    dependsOn ":env-mq-rabbit:publish"
    dependsOn ":env-mq-redis:publish"
    dependsOn ":env-mq-ibmmq:publish"
    dependsOn ":env-wiremock:publish"
}
